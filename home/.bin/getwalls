#!/usr/bin/perl

use 5.010;

use strict;
use warnings;

use File::Temp 'tempdir';
use JSON;
use LWP::UserAgent;

my $ua = LWP::UserAgent->new(agent => 'Wallpaper Snatcher/1.0');
my $response = $ua->get("http://reddit.com/r/wallpapers.json?limit=35");

if ($response->is_error) {
  say STDERR $response->status_line;
  exit 1;
}

my $page = from_json($response->content);
my $dir = tempdir(CLEANUP => 1);

chdir $dir;

my @downloads = ();
foreach my $link (@{$page->{data}{children}}) {
  my $link = $link->{data}{url};
  my $head = $ua->head($link);
  if ($head->content_type =~ /^image/i) {
    push @downloads, $link;
  } else {
    if ($link =~ m{^https?://(?:.*\.)?imgur\.com/a/(\w+)(?:#\d+)?$}) {
      my $page = $ua->get($link . '/noscript')->decoded_content;
      while ($page =~ m{src="(//i\.imgur\.com/\w+\.jpg)}gc) {
        push @downloads, "http:$1";
      }
    } elsif ($link =~ m{^https?://(?:.*\.)?imgur\.com/(\w+)$}) {
      push @downloads, "http://i.imgur.com/$1.jpg";
    } elsif ($link =~ m{^https?://(?:.*\.)?flickr.com/photos/(.*)/(\d+)}) {
      my $page = $ua->get("https://www.flickr.com/photos/$1/$2/sizes/h")->decoded_content;
      my ($url) = ($page =~ m{<img src="(https://farm\d+.staticflickr.com/[^"]+)">});
      push @downloads, $url if $url;
    } elsif ($link =~ m{^https?://(?:.*\.)?500px.com}) {
      my $page = $ua->get($link)->decoded_content;
      my ($url) = ($page =~ m{src="(http://\w+cdn.500px.org/[^"]+)"});
      push @downloads, $url if $url;
    } else {
      say STDERR "Unsupported url: $link";
    }
  }
}

printf "Downloading %u files\n", scalar @downloads;

system('wget', '-q', $_) foreach @downloads;
system('rm *.1');
system('cp', $_, "$ENV{HOME}/Downloads/wallpaper") for split /\n/, qx{sxiv -tor .};

chdir $ENV{HOME};
